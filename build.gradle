buildscript {
    ext {
        springBootVersion = "2.1.+"
        springDependencyVersion = "1.0.6.RELEASE"
        shadowPluginVersion = "2.0.4"
        jmhVersion = "1.21"
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
        classpath "io.spring.gradle:dependency-management-plugin:$springDependencyVersion"
        classpath "com.github.jengelman.gradle.plugins:shadow:$shadowPluginVersion"
    }

    repositories {
        jcenter()
    }
}

allprojects {
    apply plugin: "java"

    group 'com.feb29.springboot.example'
    version '0.1.0-SNAPSHOT'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
    }
}


project(":bootiful") {

    apply plugin: "io.spring.dependency-management"
    apply plugin: "org.springframework.boot"

    bootJar {
        classifier = "boot"
        mainClassName = "com.feb29.springboot.example.bootiful.DemoApplication"
    }

    dependencies {
        compile("org.springframework.boot:spring-boot-starter-web")
    }
}

project(":benchmark") {
    apply plugin: "com.github.johnrengelman.shadow"

    Task bootJarTask = rootProject.allprojects
            .find { it.pluginManager.hasPlugin("org.springframework.boot") }
            .bootJar

    dependencies {
        compile "org.openjdk.jmh:jmh-core:$jmhVersion"
        compile "org.openjdk.jmh:jmh-generator-annprocess:$jmhVersion"
        runtime bootJarTask.outputs.files
    }
    shadowJar {
        entryCompression = org.gradle.api.tasks.bundling.ZipEntryCompression.STORED
        manifest {
            attributes(
                    "Main-Class": "org.openjdk.jmh.Main",
                    "Spring-Boot-Classes": "BOOT-INF/classes/",
                    "Spring-Boot-Lib": "BOOT-INF/lib/",
                    "Start-Class": bootJarTask.mainClassName
            )
        }
    }
}